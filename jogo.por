programa {
    inclua biblioteca Util --> u
    inclua biblioteca Matematica --> mat

    // DECLARA√á√ïES GLOBAIS
    cadeia nomeHeroi
    inteiro indiceClasseHeroi // 0-Guerreiro 1-Mago 2-Arqueiro
    inteiro vidaHeroiAtual
    inteiro vidaHeroiMaxima
    inteiro ataqueHeroiAtual
    inteiro defesaHeroiAtual
    inteiro xpHeroiAtual
    inteiro nivelHeroiAtual
    inteiro xpParaProximoNivel

    // Classes Base
    cadeia NOME_CLASSE[3] = {"T√©cnico de Suspens√£o", "Eletricista Automotivo", "Mec√¢nico Geral"}
    inteiro VIDA_BASE[3] = {135, 100, 115}
    inteiro ATAQUE_BASE[3] = {25, 45, 30}
    inteiro DEFESA_BASE[3] = {15, 5, 10}

    // Inimigos
    cadeia NOME_INIMIGO[15] // 14 normais + 1 chefe
    inteiro VIDA_INIMIGO[15]
    inteiro ATAQUE_INIMIGO[15]
    inteiro DEFESA_INIMIGO[15]
    inteiro XP_INIMIGO[15]

    // Regi√µes
cadeia NOME_REGIAO[5] = {
    "Dep√≥sito Enferrujado",
    "Bairro dos Quebrados",
    "T√∫nel das Correias",
    "Sucat√£o do Caos",
    "Oficina Federal"
}
cadeia DESCRICAO_REGIAO[5] = {
    "Voc√™ entra no Dep√≥sito Enferrujado, um labirinto de pe√ßas velhas, √≥leo no ch√£o e barulhos estranhos vindos dos cantos escuros. Algo parece te observar entre os chassis abandonados.",
    "O Bairro dos Quebrados √© onde carros v√™m para morrer ‚Äî ou para serem revividos nas coxas. √â territ√≥rio de gambiarreiros e clientes desesperados. O risco de calote √© alto por aqui.",
    "O T√∫nel das Correias ecoa com barulhos de rolamentos soltos e motores falhando. Umidade, fuma√ßa e pe√ßas rolando criam um ambiente tenso. Muitos mec√¢nicos se perdem nesse labirinto sonoro.",
    "Sucat√£o do Caos: um campo minado de carca√ßas e motores partidos. Aqui, tudo √© reaproveitado ‚Äî at√© os monstros. O cheiro de solda e a energia el√©trica no ar fazem os cabelos do bra√ßo arrepiarem.",
    "A Oficina Federal se ergue como uma fortaleza de burocracia, taxas e terror fiscal. Dentro, o **Devastador** ‚Äî avatar supremo da Receita Federal ‚Äî te espera com seus autos de infra√ß√£o e scanner de alma."
    }
    inteiro regiaoAtual
    logico jogoEmAndamento

    // FUN√á√ÉO PRINCIPAL
    funcao inicio() {
        configurarInimigos()
        contarHistoria()
    }

    // FUN√á√ïES DE CONFIGURA√á√ÉO INICIAL 
    funcao configurarInimigos() {

    NOME_INIMIGO[0] = "Po√ßa de √ìleo Viva"
    VIDA_INIMIGO[0] = 20
    ATAQUE_INIMIGO[0] = 5
    DEFESA_INIMIGO[0] = 0
    XP_INIMIGO[0] = 10

    NOME_INIMIGO[1] = "Pneu Vingativo"
    VIDA_INIMIGO[1] = 40
    ATAQUE_INIMIGO[1] = 12
    DEFESA_INIMIGO[1] = 2
    XP_INIMIGO[1] = 15

    NOME_INIMIGO[2] = "Vela de Igni√ß√£o Infernal"
    VIDA_INIMIGO[2] = 30
    ATAQUE_INIMIGO[2] = 18
    DEFESA_INIMIGO[2] = 1
    XP_INIMIGO[2] = 20

    NOME_INIMIGO[3] = "Mec√¢nico de YouTube"
    VIDA_INIMIGO[3] = 60
    ATAQUE_INIMIGO[3] = 15
    DEFESA_INIMIGO[3] = 5
    XP_INIMIGO[3] = 40

    NOME_INIMIGO[4] = "Motor Raivoso"
    VIDA_INIMIGO[4] = 100
    ATAQUE_INIMIGO[4] = 20
    DEFESA_INIMIGO[4] = 10
    XP_INIMIGO[4] = 60

    NOME_INIMIGO[5] = "Monstro da Ferrugem"
    VIDA_INIMIGO[5] = 90
    ATAQUE_INIMIGO[5] = 10 
    DEFESA_INIMIGO[5] = 15
    XP_INIMIGO[5] = 50

    NOME_INIMIGO[6] = "Lombada Assassina"
    VIDA_INIMIGO[6] = 50
    ATAQUE_INIMIGO[6] = 45
    DEFESA_INIMIGO[6] = 20
    XP_INIMIGO[6] = 55

    NOME_INIMIGO[7] = "Uno com Escada"
    VIDA_INIMIGO[7] = 85
    ATAQUE_INIMIGO[7] = 30
    DEFESA_INIMIGO[7] = 20
    XP_INIMIGO[7] = 60

    NOME_INIMIGO[8] = "Carro Possu√≠do"
    VIDA_INIMIGO[8] = 200
    ATAQUE_INIMIGO[8] = 30
    DEFESA_INIMIGO[8] = 10
    XP_INIMIGO[8] = 90

    NOME_INIMIGO[9] = "Golem de Sucata"
    VIDA_INIMIGO[9] = 250
    ATAQUE_INIMIGO[9] = 30
    DEFESA_INIMIGO[9] = 30
    XP_INIMIGO[9] = 100

    NOME_INIMIGO[10] = "Guincho Abutre"
    VIDA_INIMIGO[10] = 110
    ATAQUE_INIMIGO[10] = 20
    DEFESA_INIMIGO[10] = 10
    XP_INIMIGO[10] = 90

    NOME_INIMIGO[11] = "Fiscal do DETRAN Sinistro"
    VIDA_INIMIGO[11] = 150
    ATAQUE_INIMIGO[11] = 25
    DEFESA_INIMIGO[11] = 10
    XP_INIMIGO[11] = 100

    NOME_INIMIGO[12] = "Rei da Gambiarra"
    VIDA_INIMIGO[12] = 200
    ATAQUE_INIMIGO[12] = 30
    DEFESA_INIMIGO[12] = 15
    XP_INIMIGO[12] = 120

    NOME_INIMIGO[13] = "Carn√™ do IPVA Maldito"
    VIDA_INIMIGO[13] = 100
    ATAQUE_INIMIGO[13] = 30
    DEFESA_INIMIGO[13] = 5
    XP_INIMIGO[13] = 50

    NOME_INIMIGO[14] = "Receita Federal"
    VIDA_INIMIGO[14] = 500
    ATAQUE_INIMIGO[14] = 40
    DEFESA_INIMIGO[14] = 25
    XP_INIMIGO[14] = 0
}

    funcao contarHistoria(){
        cadeia opcao
        
        escreva("\n")
        escreva("Pular Hist√≥ria?\n")
        escreva("\n")
        escreva("1-Sim\n")
        escreva("2-N√£o\n")
        escreva("\nEscolha uma op√ß√£o: ")
        leia(opcao)

        se (opcao == 1) {
            limpa()
            u.aguarde(500)
            menu()
        }
        senao se (opcao == 2) {
            limpa()
            u.aguarde(1000)
            escreva("\nH√° tempos, a Oficina Central era o cora√ß√£o pulsante da cidade, onde carros ganhavam vida e motores cantavam...\n")
            u.aguarde(11000)
            limpa()
            escreva("\nMas a burocracia tem sua pr√≥pria engrenagem, lenta e implac√°vel...\n")
            u.aguarde(6500)
            limpa()
            escreva("\nDas sombras dos impostos e multas surgiu a temida Receita Federal...\n")
            u.aguarde(6500)
            limpa()
            escreva("\nOs carros pararam, as ferramentas enferrujaram, e a esperan√ßa quase se perdeu...\n")
            u.aguarde(6500)
            limpa()
            escreva("\nMas nem todo motor cessa sua igni√ß√£o...\n")
            u.aguarde(4000)
            limpa()
            escreva("\nNo meio desse caos, uma lenda desperta... um mec√¢nico... esse mec√¢nico... √© voc√™.\n")
            u.aguarde(6500)
            escreva("\nVoc√™ deve encarar as cinco zonas de desafios, desde sucateiros at√© caloteiros. O destino da Oficina Central depende da sua habilidade.\n")
            u.aguarde(11000)
            limpa()
            escreva("\nPrepare suas ferramentas...\n")
            u.aguarde(2500)
            escreva("\nSua jornada come√ßa agora!\n")
            u.aguarde(2500)
            limpa()
            menu()
        }
        senao {
            escreva("Op√ß√£o inv√°lida.\n")
            u.aguarde(1000)
            limpa()
            contarHistoria()
            retorne
        }
    }

    funcao menu() {
        cadeia opcao

        faca {
            limpa()
            escreva("\n")
            escreva("‚îå‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îê\n")
            escreva("‚îÇ   A JORNADA DO MEC√ÇNICO    ‚îÇ\n")
            escreva("‚îî‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îò\n")
            escreva("‚îå‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îê\n")
            escreva("‚îÇ   1. Novo Jogo             ‚îÇ\n")
            escreva("‚îÇ   2. Cr√©ditos              ‚îÇ\n")
            escreva("‚îÇ   3. Sair                  ‚îÇ\n")
            escreva("‚îî‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îò\n")
            escreva("\nEscolha uma op√ß√£o: ")
            leia(opcao)

            se (opcao == 1) {
                novoJogo() 
            }
            senao se (opcao == 2) {
                creditos()
                u.aguarde(5000)
            }
            senao se (opcao == 3) {
                limpa()
                escreva("\n")
                escreva("Saindo do jogo.")
                u.aguarde(500)
                limpa()
                escreva("\n")
                escreva("Saindo do jogo..")
                u.aguarde(500)
                limpa()
                escreva("\n")
                escreva("Saindo do jogo...")
                u.aguarde(500)
            }
            senao {
                escreva("Op√ß√£o inv√°lida.\n")
                u.aguarde(1500)
            }
        } enquanto (opcao != 3)
    }


    funcao creditos() {
        limpa()
        escreva("\n")
        escreva("‚îå‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îê\n")
        escreva("‚îÇ          CR√âDITOS          ‚îÇ\n")
        escreva("‚îú‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚î§\n")
        escreva("‚îÇ Desenvolvedor: Giovani.S   ‚îÇ\n")
        escreva("‚îÇ Assistente AI: Gemini      ‚îÇ\n")
        escreva("‚îÇ Ano: 2025                  ‚îÇ\n")
        escreva("‚îî‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îò\n")
    }
       
       funcao novoJogo() {
        carregamentoJogo()
        criacaoDoHeroi()

        regiaoAtual = 0
        jogoEmAndamento = verdadeiro
        iniciarJornada()
    }
    
    funcao carregamentoJogo() {
        limpa()
        escreva("\n")
        escreva("Iniciando novo jogo.") 
        u.aguarde(500)
        limpa()
        escreva("\n")
        escreva("Iniciando novo jogo..") 
        u.aguarde(500)
        limpa()
        escreva("\n") 
        escreva("Iniciando novo jogo...") u.aguarde(500)
    }

    funcao criacaoDoHeroi() {
        cadeia escolhaTexto // Ler como texto primeiro
        inteiro escolhaClasseNumerica = 0 // Armazenar o valor

        limpa()
        escreva("\n")
        escreva("‚îå‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îê\n")
        escreva("‚îÇ      CRIA√á√ÉO DO MEC√ÇNICO      ‚îÇ\n")
        escreva("‚îú‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚î§\n")
        escreva("‚îÇ Digite o nome do seu mec√¢nico:‚îÇ\n")
        escreva("‚îî‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îò\n")
        escreva("\nNome: ")
        leia(nomeHeroi)
        limpa()

        faca {
            escreva("\n")
            escreva("‚îå‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îê\n")
            escreva("‚îÇ                ESCOLHA SUA CLASSE                ‚îÇ\n")
            escreva("‚îú‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚î§\n")
            escreva("‚îÇ 1. T√©cnico de Suspens√£o     HP:", VIDA_BASE[0], " ATK:", ATAQUE_BASE[0], " DEF:", DEFESA_BASE[0]," ‚îÇ\n")
            escreva("‚îÇ 2. Eletricista Automotivo   HP:", VIDA_BASE[1], "  ATK:", ATAQUE_BASE[1], " DEF:", DEFESA_BASE[1]," ‚îÇ\n")
            escreva("‚îÇ 3. Mec√¢nico Geral           HP:", VIDA_BASE[2], " ATK:", ATAQUE_BASE[2], " DEF:", DEFESA_BASE[2]," ‚îÇ\n")
            escreva("‚îî‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îò\n")
            escreva("\nEscolha uma op√ß√£o (1-3): ")
            leia(escolhaTexto)

            // Validar e converter
            se (escolhaTexto == "1") {
                escolhaClasseNumerica = 1
            } senao se (escolhaTexto == "2") {
                escolhaClasseNumerica = 2
            } senao se (escolhaTexto == "3") {
                escolhaClasseNumerica = 3
            } senao {
                escolhaClasseNumerica = 0 // Sinaliza inv√°lido
            }

            se (escolhaClasseNumerica >= 1 e escolhaClasseNumerica <= 3) {
                indiceClasseHeroi = escolhaClasseNumerica - 1 // Usa o valor num√©rico

                vidaHeroiAtual = VIDA_BASE[indiceClasseHeroi]
                vidaHeroiMaxima = VIDA_BASE[indiceClasseHeroi]
                ataqueHeroiAtual = ATAQUE_BASE[indiceClasseHeroi]
                defesaHeroiAtual = DEFESA_BASE[indiceClasseHeroi]
                xpHeroiAtual = 0
                nivelHeroiAtual = 1
                xpParaProximoNivel = 75 * nivelHeroiAtual

                heroiCriadoComSucesso()
                u.aguarde(5000)
            } senao {
                escreva("Op√ß√£o inv√°lida. Digite um n√∫mero entre 1 e 3.\n")
                u.aguarde(1000)
                limpa()
            }
        } enquanto (escolhaClasseNumerica < 1 ou escolhaClasseNumerica > 3)
    }

    funcao heroiCriadoComSucesso(){
        limpa()
        escreva("\n")
        escreva("Seu mec√¢nico foi criado!\n\n")
        escreva("Nome: ", nomeHeroi, "\n")
        escreva("Classe: ", NOME_CLASSE[indiceClasseHeroi], "\n")
        escreva("Vida: ", vidaHeroiAtual, "/", vidaHeroiMaxima, "\n")
        escreva("Ataque: ", ataqueHeroiAtual, "\n")
        escreva("Defesa: ", defesaHeroiAtual, "\n")
        escreva("N√≠vel: ", nivelHeroiAtual, "\n")
        escreva("XP: ", xpHeroiAtual, "/", xpParaProximoNivel, "\n")
    }
    
    funcao iniciarJornada() {
        limpa()
        escreva("\n")
        escreva("A jornada de ", nomeHeroi, " come√ßa agora!\n")
        u.aguarde(3000)

        enquanto (jogoEmAndamento e regiaoAtual < 5) {
            explorarRegiao()

            se (nao jogoEmAndamento) {
                retorne
            }

            // Se n√£o for a √∫ltima regi√£o e o jogo ainda estiver em andamento
            se (regiaoAtual < 4 e jogoEmAndamento) {
                sortearEventoEspecialPosRegiao()
            }

            se (jogoEmAndamento) {
                 regiaoAtual++
                 // Se regiaoAtual virar 5 o chefe foi derrotado e o loop enquanto vai parar na pr√≥xima verifica√ß√£o
            }
        }
    }

    funcao explorarRegiao() {
        limpa()
        escreva("\n")
        escreva("--- ", NOME_REGIAO[regiaoAtual], " ---\n")
        escreva(DESCRICAO_REGIAO[regiaoAtual], "\n\n")
        u.aguarde(10000)

        se (regiaoAtual == 4) { // Oficina Federal - Batalha contra o Chefe
            escreva("Voc√™ sente uma presen√ßa maligna avassaladora...\n")
            u.aguarde(4000)
            escreva("A Receita Federal est√° √† sua frente!\n")
            u.aguarde(4000)
            batalha(14) // √çndice 14 √© O "Devastador"
        } senao {
            inteiro numBatalhas = u.sorteia(1, 5)
            escreva("Voc√™ encontrar√° ", numBatalhas, " amea√ßa(s) nesta regi√£o.\n")
            u.aguarde(2000)

            para (inteiro i = 1; i <= numBatalhas; i++) {
                se (nao jogoEmAndamento) {
                    retorne
                }
                limpa()
                escreva("--- Batalha ", i, " de ", numBatalhas, " em ", NOME_REGIAO[regiaoAtual], " ---\n")
                u.aguarde(1500)

                inteiro indiceInimigoSorteado = u.sorteia(0, 13)
                batalha(indiceInimigoSorteado)

                se (nao jogoEmAndamento){
                    retorne
                }
                se(jogoEmAndamento){
                    u.aguarde(2000)
                }
            }
        }
    }

        funcao logico batalha(inteiro idInimigo) {
        // inicializa√ß√£o da batalha
        cadeia acaoHeroiTexto // Para ler a a√ß√£o como texto
        inteiro vidaInimigoAtual = VIDA_INIMIGO[idInimigo]
        inteiro ataqueInimigo = ATAQUE_INIMIGO[idInimigo]
        inteiro defesaInimigo = DEFESA_INIMIGO[idInimigo]
        cadeia nomeInimigoBatalha = NOME_INIMIGO[idInimigo]
        inteiro turno = 1
        logico heroiVenceu = falso

        limpa()
        escreva("\n")
        escreva("‚öîÔ∏è BATALHA INICIADA! ‚öîÔ∏è\n")
        escreva(nomeHeroi, " (HP: ", vidaHeroiAtual, "/", vidaHeroiMaxima, ") vs ", nomeInimigoBatalha, " (HP: ", vidaInimigoAtual, "/", VIDA_INIMIGO[idInimigo], ")\n\n")
        u.aguarde(5000)
        limpa()

        enquanto (vidaHeroiAtual > 0 e vidaInimigoAtual > 0) {
            escreva("\n")
            escreva("--- Turno ", turno, " ---")
            escreva("\n", nomeHeroi, " (HP: ", vidaHeroiAtual, "/", vidaHeroiMaxima, ")  |  ", nomeInimigoBatalha, " (HP: ", vidaInimigoAtual, "/", VIDA_INIMIGO[idInimigo],")\n")
            escreva("\n")
            escreva("1. Atacar\n")

            faca{
                escreva("Escolha sua a√ß√£o: ")
                leia(acaoHeroiTexto) // L√™ como texto
                se(acaoHeroiTexto != "1"){ // Compara com o texto "1"
                    escreva("Op√ß√£o inv√°lida! Pressione 1 para atacar.\n")
                    u.aguarde(1000)
                }
            } enquanto(acaoHeroiTexto != "1")

                inteiro danoCausado = ataqueHeroiAtual - defesaInimigo
                se (danoCausado < 1) {
                    danoCausado = u.sorteia(1, 3)
                }
                vidaInimigoAtual -= danoCausado
                escreva("\n")
                escreva(nomeHeroi, " ataca ", nomeInimigoBatalha, " causando ", danoCausado, " de dano!\n")
                u.aguarde(1500)

                se (vidaInimigoAtual <= 0) {
                    vidaInimigoAtual = 0
                    escreva(nomeInimigoBatalha, " foi derrotado!\n")
                    heroiVenceu = verdadeiro
                    u.aguarde(1500)
                    se (idInimigo != 14) { 
                        ganharXP(XP_INIMIGO[idInimigo])
                    }
                }

            // Turno do Inimigo, etc...
            se (vidaInimigoAtual > 0 e vidaHeroiAtual > 0) {
                escreva("\nVez de ", nomeInimigoBatalha, "!\n")
                u.aguarde(1000)

                se (idInimigo == 14 e turno % 2 == 0) { 
                    inteiro curaChefe = u.sorteia(20, 40) 
                    vidaInimigoAtual += curaChefe
                    se (vidaInimigoAtual > VIDA_INIMIGO[idInimigo]) {
                        vidaInimigoAtual = VIDA_INIMIGO[idInimigo]
                    }
                    escreva(nomeInimigoBatalha, " arrecada os tributos devidos e recupera ", curaChefe, " de HP!\n")
                    escreva(nomeInimigoBatalha, " Vida: ", vidaInimigoAtual, "/", VIDA_INIMIGO[idInimigo], "\n")
                    u.aguarde(2000)
                }

                inteiro danoRecebido = ataqueInimigo - defesaHeroiAtual
                se (danoRecebido < 1) {
                    danoRecebido = u.sorteia(1, 3) 
                }
                vidaHeroiAtual -= danoRecebido
                escreva(nomeInimigoBatalha, " ataca ", nomeHeroi, " causando ", danoRecebido, " de dano!\n")
                u.aguarde(1500)

                se (vidaHeroiAtual <= 0) {
                    vidaHeroiAtual = 0
                    escreva(nomeHeroi, " foi derrotado...\n")
                    u.aguarde(1500)
                    gameOver() 
                    retorne falso 
                }
            }
            limpa()
            turno++
            escreva("\n")
            u.aguarde(1000)
        } // Fim do while da batalha

        se (heroiVenceu) {
            se (idInimigo == 14) { 
                jogoVencido() 
                retorne verdadeiro
            }
            retorne verdadeiro 
        }
        
        retorne falso
    }
    
    funcao ganharXP(inteiro xpGanha) {
        escreva("\nVoc√™ ganhou ", xpGanha, " XP!\n")
        xpHeroiAtual += xpGanha
        u.aguarde(1500)
        verificarSubiuDeNivel()
    }

    funcao verificarSubiuDeNivel() {
        enquanto (xpHeroiAtual >= xpParaProximoNivel) {
            nivelHeroiAtual++
            xpHeroiAtual -= xpParaProximoNivel
            xpParaProximoNivel = (75 + (nivelHeroiAtual * 25)) * nivelHeroiAtual 

            limpa()
            escreva("üéâ PARAB√âNS! Voc√™ subiu para o N√≠vel ", nivelHeroiAtual, "! üéâ\n")
            u.aguarde(2000)
            
            vidaHeroiMaxima += 5 + u.sorteia(1,5) 
            ataqueHeroiAtual += 1 
            defesaHeroiAtual += 1 
            escreva("Seus atributos base aumentaram um pouco!\n")
            u.aguarde(1500)
            
            escolherMelhoriaAtributo() 

            vidaHeroiAtual = vidaHeroiMaxima 
            escreva("Sua vida foi totalmente restaurada!\n")
            u.aguarde(1500)

            escreva("\nXP Atual: ", xpHeroiAtual, "/", xpParaProximoNivel, "\n")
            u.aguarde(2000)
            exibirStatusHeroi() 
        }
    }

    funcao escolherMelhoriaAtributo() {
        cadeia escolhaTexto
        inteiro escolhaNumerica = 0 // Guardar a op√ß√£o v√°lida
        faca {
            limpa()
            escreva("Escolha um atributo principal para melhorar:\n")
            escreva("1. Vitalidade (+", (10 + nivelHeroiAtual * 2) ," Vida M√°xima)\n") 
            escreva("2. For√ßa (+", (2 + nivelHeroiAtual / 2) ," Ataque)\n")
            escreva("3. Resist√™ncia (+", (1 + nivelHeroiAtual / 3) ," Defesa)\n")
            escreva("Op√ß√£o: ")
            leia(escolhaTexto)

            inteiro bonusVida = 10 + nivelHeroiAtual * 2
            inteiro bonusAtaque = 2 + nivelHeroiAtual / 2
            inteiro bonusDefesa = 1 + nivelHeroiAtual / 3

            se (escolhaTexto == "1") {
                escolhaNumerica = 1
                vidaHeroiMaxima += bonusVida
                escreva("Vida M√°xima aumentada em ", bonusVida, "!\n")
            } senao se (escolhaTexto == "2") {
                escolhaNumerica = 2
                ataqueHeroiAtual += bonusAtaque
                escreva("Ataque aumentado em ", bonusAtaque, "!\n")
            } senao se (escolhaTexto == "3") {
                escolhaNumerica = 3
                defesaHeroiAtual += bonusDefesa
                escreva("Defesa aumentada em ", bonusDefesa, "!\n")
            } senao {
                escolhaNumerica = 0 // Inv√°lido
                escreva("Op√ß√£o inv√°lida. Tente novamente.\n")
                u.aguarde(1000)
            }
        } enquanto (escolhaNumerica < 1 ou escolhaNumerica > 3)
        u.aguarde(1500)
    }

    funcao exibirStatusHeroi() {
        limpa()
        escreva("--- STATUS DO HER√ìI ---\n")
        escreva("Nome: ", nomeHeroi, "\n")
        escreva("Classe: ", NOME_CLASSE[indiceClasseHeroi], "\n")
        escreva("N√≠vel: ", nivelHeroiAtual, "\n")
        escreva("Vida: ", vidaHeroiAtual, "/", vidaHeroiMaxima, "\n")
        escreva("Ataque: ", ataqueHeroiAtual, "\n")
        escreva("Defesa: ", defesaHeroiAtual, "\n")
        escreva("XP: ", xpHeroiAtual, "/", xpParaProximoNivel, "\n")
        escreva("-----------------------\n")
        u.aguarde(3500)
    }

    funcao sortearEventoEspecialPosRegiao() {
        inteiro sorteio = u.sorteia(1, 100)

        se (sorteio <= 30) { 
            limpa()
            escreva("‚ú® Evento Especial! ‚ú®\n")
            escreva("Voc√™ encontrou uma Po√ß√£o de Cura potente!\n")
            inteiro cura = vidaHeroiMaxima / 2 
            vidaHeroiAtual += cura
            se (vidaHeroiAtual > vidaHeroiMaxima) {
                vidaHeroiAtual = vidaHeroiMaxima
            }
            escreva("Sua vida foi restaurada em ", cura, " HP. Vida atual: ", vidaHeroiAtual, "/", vidaHeroiMaxima, "\n")
            u.aguarde(3000)
        } senao se (sorteio > 30 e sorteio <= 55) { 
            limpa()
            escreva("‚ú® Evento Especial! ‚ú®\n")
            escreva("Voc√™ encontrou um local seguro e aben√ßoado para descansar.\n")
            vidaHeroiAtual = vidaHeroiMaxima 
            escreva("Voc√™ se sente revigorado! Vida totalmente restaurada: ", vidaHeroiAtual, "/", vidaHeroiMaxima, "\n")
            u.aguarde(3000)
        } senao se (sorteio > 55 e sorteio <= 75) { 
            limpa()
            escreva("‚ú® Evento Especial! ‚ú®\n")
            escreva("Um esp√≠rito ancestral aparece e oferece uma b√™n√ß√£o...\n")
            escreva("1. B√™n√ß√£o da Sabedoria (XP B√¥nus)\n")
            escreva("2. B√™n√ß√£o da Fortitude (Aumento tempor√°rio de Defesa para a pr√≥xima regi√£o)\n")
            cadeia escolhaRecompensaTexto
            inteiro escolhaRecompensaNumerica = 0
            faca {
                escreva("Escolha (1 ou 2): ")
                leia(escolhaRecompensaTexto)
                se (escolhaRecompensaTexto == "1") {
                    escolhaRecompensaNumerica = 1
                    inteiro xpBonus = xpParaProximoNivel / 3 
                    escreva("Voc√™ sente sua mente se expandir e recebe ", xpBonus, " XP b√¥nus!\n")
                    ganharXP(xpBonus)
                } senao se (escolhaRecompensaTexto == "2") {
                    escolhaRecompensaNumerica = 2
                    inteiro defBonus = 3 + nivelHeroiAtual / 2
                    defesaHeroiAtual += defBonus
                    escreva("Sua pele enrijece! Defesa aumentada em ", defBonus, " para os pr√≥ximos desafios!\n")
                } senao {
                    escolhaRecompensaNumerica = 0
                    escreva("Op√ß√£o inv√°lida.\n")
                    u.aguarde(1000)
                }
            } enquanto (escolhaRecompensaNumerica < 1 ou escolhaRecompensaNumerica > 2)
            u.aguarde(3000)
        }
    }

    funcao gameOver() {
        limpa()
        escreva("üíÄ GAME OVER üíÄ\n\n")
        escreva("Sua jornada termina aqui, ", nomeHeroi, ".\n")
        escreva("A Oficina Central permanece nas sombras...\n\n")
        escreva("Pressione Enter para voltar ao menu...")
        cadeia temp 
        leia(temp)
        jogoEmAndamento = falso
    }

    funcao jogoVencido() {
        limpa()
        escreva("üèÜ VIT√ìRIA! üèÜ\n\n")
        escreva("Parab√©ns, ", nomeHeroi, "!\n")
        escreva("Voc√™ derrotou a Receita Federal e a Pedra da Luz foi recuperada!\n")
        escreva("A Oficina Central est√° a salvo gra√ßas √† sua bravura!\n")
        escreva("Sua lenda ser√° contada por eras!\n\n")
        u.aguarde(5000)
        creditos()
        u.aguarde(4000)
        escreva("\nPressione Enter para voltar ao menu...")
        cadeia temp 
        leia(temp)
        jogoEmAndamento = falso
    }
}
